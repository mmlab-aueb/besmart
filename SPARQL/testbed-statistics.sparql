#Get statistics per phenomena
Prefix ssn: <http://purl.oclc.org/NET/ssnx/ssn#> 
Prefix iotlite: <http://purl.oclc.org/NET/UNIS/fiware/iot-lite#> 
Prefix dul: <http://www.loa.istc.cnr.it/ontologies/DUL.owl#> 
Prefix geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
Prefix time: <http://www.w3.org/2006/time#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
Prefix m3-lite: <http://purl.org/iot/vocab/m3-lite#>
Prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select ?sensorID ?type (COUNT(?value) AS ?measurements)
where { 
    ?o a ssn:Observation.
    ?o ssn:observedBy ?sensorID. 
    ?o ssn:observedProperty ?qkr.
    ?qkr rdf:type ?type.
    #Values ?type {m3-lite:AirTemperature}
    ?o geo:location ?point. 
    ?point geo:lat ?latitude. 
    ?point geo:long ?longitude.
    ?o ssn:observationResult ?or. 
    ?or ssn:hasValue ?v. 
    ?v dul:hasDataValue ?value.

 
 #SMARTSANTANDER
  FILTER ( 
      (xsd:double(?latitude) >= "43.45000"^^xsd:double) 
   && (xsd:double(?latitude) <= "43.48000"^^xsd:double) 
   && ( xsd:double(?longitude) >= "-3.90000"^^xsd:double)  
   && ( xsd:double(?longitude) <= "-3.70000"^^xsd:double)
   )

} GROUP BY  ?sensorID ?type
order by DESC(COUNT(?value))
